
private struct MapEntry$<K, V> {
    var k: K;
    var v: V;
    var next: own*? MapEntry$<K, V>;
    var previous: *? MapEntry$<K, V>;
}

private struct MapEntryList$<K, V> {
    var list: LinkedList$<MapEntry$<K,V>>;
}

struct HashMap$<K, V> {
    private var table: DArray$<MapEntryList$<K,V>> = DArray$<MapEntryList$<K,V>>{ .resize(4); };
    var defValue: V;
    private var length: Int = 0;
    
    private fun constGetEntryList(k: raw* const K) const : * const MapEntryList$<K,V> {
        var h = hashCode$<K>(k);
        h %= table.size();
        return table.constGet(h);
    }

    private fun getEntryList(k: raw* const K) : * MapEntryList$<K,V> {
        var h = hashCode$<K>(k);
        h %= table.size();
        return table.get(h);
    }

    fun get(k: K): V {
        var list = constGetEntryList(&k);
        for (var itr = list.list.constFirst(); itr != null; itr = itr.next) {
            if (compare$<K>(&itr.k, &k) == 0) {
                return itr.v;
            }
        }
        return defValue;
    }
    
    fun set(k: mut K, v: mut V) : Bool {
        var list = getEntryList(&k);
        for (var itr = list.list.first(); itr != null; itr = itr.next) {
            if (compare$<K>(&itr.k, &k) == 0) {
                itr.v = move v;
                return false;
            }
        }
        var entry = alloc$<MapEntry$<K,V>>() { .k = move k; .v = move v; };
        list.list.add(move entry);
        ++length;
        rehash();
        return true;
    }
    
    fun size() const : Int {
        return length;
    }

    fun clear() {
        for (var i=0; i<table.size(); ++i) {
            table[i].list.clear();
        }
        length = 0;
    }
    
    fun remove(k: K) : Bool {
        var list = getEntryList(&k);
        for (var itr = list.list.first(); itr != null; itr = itr.next) {
            if (compare$<K>(&itr.k, &k) == 0) {
                list.list.remove(itr);
                ++length;
                return true;
            }
        }
        return false;
    }
    
    fun contains(k: K) const : Bool {
        var list = constGetEntryList(&k);
        for (var itr = list.list.constFirst(); itr != null; itr = itr.next) {
            if (compare$<K>(&itr.k, &k) == 0) {
                return true;
            }
        }
        return false;
    }
    
    fun eachWhile(f: fun(v:raw* const V, k:raw* const K):Bool) const {
        for (var i = 0; i<table.size(); ++i) {
            var list = table.constGet(i);
            for (var itr = list.list.constFirst(); itr != null; itr = itr.next) {
                if (!f(&itr.v, &itr.k)) {
                    return;
                }
            }
        }
    }

    private fun rehash() {
        if (length < table.size()) {
            return;
        }
        var newSize = length < 256 ? length*2+8 : (length*1.5) as Int;
        var otable = DArray$<MapEntryList$<K,V>>{ .resize(newSize); };
        otable.swap(&this.table);
        length = 0;

        for (var i = 0; i<otable.size(); ++i) {
            var list = otable.get(i);
            for (var itr = list.list.first(); itr != null; itr = itr.next) {
                set(move itr.k, move itr.v);
            }
        }
    }
}